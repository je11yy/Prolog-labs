#№ Отчет по лабораторной работе №4
## по курсу "Логическое программирование"

## Обработка естественного языка

### студент: Власова Л.А.

## Результат проверки

| Преподаватель     | Дата         |  Оценка       |
|-------------------|--------------|---------------|
| Сошников Д.В. |              |               |
| Левинская М.А.|              |               |

> *Комментарии проверяющих (обратите внимание, что более подробные комментарии возможны непосредственно в репозитории по тексту программы)*


## Введение

Естественный язык представляет собой сложную систему, требующую последовательного анализа, включающего поиск границ слов и предложений, определение грамматических форм слов, выявление синтаксических связей и раскрытие семантического содержания. Все это позволяет выявить закономерности языка. Данная работа использует экспертный лингвистический подход для исследования ограниченной части языка и выделения грамматических правил. Грамматика, разрабатываемая в рамках данной работы, будет определяться терминальными и нетерминальными элементами и правилами перехода, учитывающими контекст. Пролог был выбран для описания грамматики из-за его удобства как для чтения, так и для написания человеком.

## Задание

8. Реализовать преобразователь активных и пассивных форм типа:
[’Саша’, ’и’, ’Лена’, ’любят’, ‘шоколад’] и
[’шоколад’, ’любим’, ’Сашей’, ’и’, ’Петей’]
в глубинные структуры типа и сравнить полученные глубинные структуры.
```Prolog
Запрос: ?- compare([’Саша’, ’и’, ’Лена’, ’любят’, ‘шоколад’] ,
[’шоколад’, ’любим’, ’Сашей’, ’и’, ’Леной’], Ph1, Ph2, Y).
Результат: Ph1=likes([agent(’Саша’), agent(’Лена’)], object(’шоколад’)),
Ph2=likes([agent(’Саша’), agent(’Лена’)], object(’шоколад’)), Y=yes.
```
## Принцип решения

Глубинная форма - форма содержащая в себе только смысл части предложения, без морфологических признаков, не требующая пояснения форм слов.

Активная и пассивная формы отличаются тем, как действие связывает субъект и объект. Пример активной формы: Саша любит шоколад, пассивной: шоколад любим Сашей. Прекрасно видно, чем они отличаются: глагол стоит в активном или пассивном залоге и падежи существительных отличаются. Эти закономерности и лежат в основе программы.

Поскольку структура программы довольно сложна, рассмотрим сначала разбор одной фразы.

`a_phrase(P, zalog(_), L)` анализ фраз в различных формах

```prolog
% активный залог
a_phrase(P, zalog('актив'), L):-
  append([X|XT], [Y|Z], L),
  glag(X2, zalog('актив'), Y),
  compose_agent(X1, pad('именит'), [X|XT]),
  compose_agent(X3, pad('винит'), Z),
  P = [X2, X1 ,X3].

% пассивный залог
a_phrase(P, zalog('пассив'),L):-
  append([X|XT], [Y|Z], L),
  glag(X2, zalog('пассив'), Y),
  compose_agent(X1, pad('именит'), [X|XT]),
  compose_agent(X3, pad('творит'), Z),
  P = [X2, X3 ,X1].
```  
Фраза задается списком L, который разбивается на три части: глагол, объект, субъект (все они непустые).

`compose_agent(Answer, zalog(_), Text)` составление группы существительных (возможно, соединенных союзом и)

```prolog
% несколько существтельных соединенных союзом
compose_agent([X|Ans], K,[H,Y|T]):-
  agent(X, K, H),
  union(Y),
  compose_agent(Ans, K, T).
% одно существительное
compose_agent([X], K,[H]):-
  agent(X, K, H).
```  
Из фразы формируется список с существительными в глубинной форме (союз в список не входит).

`agent(XC, K, X)` Получение глубинной формы существительного

`glag(XC, K, X)` Получение глубинной формы глагола

```prolog
% предикат существительного
agent(XC, K, X):-
  gen_mest(File_mest),
  fid(X, XC, K, File_mest).

% предикат глагола
glag(XC, K, X):-
  gen_glag(File_glag),
  fid(X, XC, K, File_glag).
```
Генерируется словарь и вызывается функция поиска слова в словаре.

`fid(Word, Ans, Form, File)` Поиск слова по всему словарю.

`condition(Word, Ans, Form, Ans:Form:List)` Поиск и выделение глубинной формы слова по конкретной строке в словаре.

```prolog
% поиск словоформы в словаре
fid(X, XC, K, File):-
  member(M, File),
  condition(X, XC, K, M).

condition(X, XC, K, XC:K:L):-
  member(X, L).
```  
Выделяется строчка словаря и для неё вызывается `condition` для выделения глубинной формы из формы `K`

Подробнее о структуре словаря:
Для структуры словаря зарезервирован оператор `op(200, xfy, ':').` Запись в словаре имеет вид:
```prolog
"Глубинная форма' : форма слова : [Список вариаций слова в этой форме]
% Например
'Маша':pad('творит'):['Машей']
'любить':zalog('пассив'):['любим', 'любима', 'любимо', 'любимы']
```

Главная процедура `compare()` - построение глубинных структур фраз и их сравнение

```prolog
% сравнение на естественном языке
% (Фраза1, Фраза2, Гл.Структура1, Гл.Структура2, Равны?)
compare(X,Y,Ph1,Ph2,Eq):-
  % выделение структуры фраз
  a_phrase(Ph1,_,X),
  a_phrase(Ph2,_,Y),
  Ph1 = [A, Obj1, Subj1],
  Ph2 = [B, Obj2, Subj2],
  % сортировка списков объектов и субъектов
  sort(Obj1,S1),
  sort(Subj1, S2),
  sort(Obj2, S3),
  sort(Subj2, S4),
  % сравнение
  cmp([A,S1,S2],[B,S3,S4],Eq).

% сравнение
%(Объект 1, Объект2, yes/no)
cmp(X,Y,Ans):-
  X = Y,
  Ans = 'yes', !.
cmp(_,_,'no').
```

В ней происходит вызов анализатора фразы для заданных фраз, затем их списки объектов и субъектов сортируются (чтобы фразы с различным их порядком следования были равными), выполняется определение их идентичности.

## Результаты
```prolog
?- compare(['Саша', 'и', 'Лена', 'любят', 'шоколад'] , ['шоколад', 'любим','Сашей', 'и', 'Леной'], Ph1,Ph2, Y).
Ph1 = Ph2, Ph2 = [likes, ['Саша', 'Лена'], [шоколад]],
Y = yes.

?- compare(['Юля', 'и', 'Глеб', 'любят', 'сосиски'] , ['сосиски', 'любимы','Глебом', 'и', 'Юлей'], Ph1,Ph2, Y).
Ph1 = [likes, ['Юля', 'Глеб'], [сосиски]],
Ph2 = [likes, ['Глеб', 'Юля'], [сосиски]],
Y = yes.

?- compare(['Саша','и','Юля', 'обожают', 'еду','и','деньги'],['деньги','и','еда','обожаемы','Юлей','и','Сашей'],Ph1,Ph2,Y).
Ph1 = [likes, ['Саша', 'Юля'], [еда, деньги]],
Ph2 = [likes, ['Юля', 'Саша'], [деньги, еда]],
Y = yes.

?- compare(['Юля', 'и', 'Глеб', 'любят', 'сосиски'] , ['сосиски', 'любимы','Глебом'], Ph1,Ph2, Y). 
Ph1 = [likes, ['Юля', 'Глеб'], [сосиски]],
Ph2 = [likes, ['Глеб'], [сосиски]],
Y = no.
```
## Выводы

После завершения лабораторной работы, мной был разработан небольшой фрагмент естественно-языкового интерфейса для модельной задачи. Была создана грамматика, охватывающая активные и пассивные формы небольшой части русского языка, с выделением соответствующих грамматических правил и слов в словаре. Таким образом, можно добавлять новые слова в этот словарь и программу будет выполнять разбор для различных слов. 
Пролог оказался удобным инструментом для этой задачи, так как реализация правил перехода оказалась интуитивной и простой.